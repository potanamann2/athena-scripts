// http://www.eathena.ws/board/index.php?showtopic=267690
// i wanna make a heal npc that is not straight heal on player,but is cast 
// santuary on ground that player stand,is that possible?

poring_c01,100,100,4	script	Healer#sanctuary	742,2,2,{
	npctalk "Can you feel my healing powers?";
	if (.npc_map$=="") donpcevent strnpcinfo(3)+"::OnInit";
	end;
	
OnInit:
	getmapxy .npc_map$,.npc_x,.npc_y, 1; // NPC coordinates
	// 5x5 area, except outer courners. 20 cells total.
	setarray .@dx[0], -1, 0, 1,-2,-1, 0, 1, 2,-2,-1, 0, 1, 2,-2,-1, 0, 1, 2,-1, 0, 1;
	setarray .@dy[0], -2,-2,-2,-1,-1,-1,-1,-1, 0, 0, 0, 0, 0, 1, 1, 1, 1, 1, 2, 2, 2;
	// setup Sanctuary cells
	for (set .@i,0; .@i<getarraysize(.@dx); set .@i,.@i+1) {
		if (checkcell(.npc_map$, .npc_x+.@dx[.@i], .npc_y+.@dy[.@i] ,cell_chkpass)) // is this check necessary?
			movenpc "sanctuary#"+.@i, .npc_x+.@dx[.@i], .npc_y+.@dy[.@i];
	}
OnTimer31000: // Sanctuary lasts 31 seconds (1 + 3*Skill_Lv)
	specialeffect 83; // Sanctuary (angel above your  head)
	donpcevent "::OnShowSanctuary"; // Sanctuary Map Unit(s)
	initnpctimer;
	end;
	
OnTouch:
	while (getmapxy(.@map$,.@x,.@y,0) == 0) {
		// check distance
		if (.npc_map$!=.@map$ || distance(.@x,.@y, .npc_x,.npc_y) > 2) break;
		if (Hp < MaxHp) {
			heal 777, 0;
			specialeffect2 312; // Heal effect
			// skilleffect "AL_HEAL",777; // but this makes them do the Heal animation too =[
		}
		sleep2 1000;
	}
	end;
}

-	script	sanctuary	139,{ 
OnShowSanctuary: specialeffect 319; // Sanctuary Map Unit
}
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#0	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#1	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#2	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#3	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#4	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#5	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#6	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#7	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#8	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#9	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#10	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#11	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#12	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#13	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#14	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#15	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#16	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#17	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#18	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#19	139
poring_c01,0,0,0	duplicate(sanctuary)	sanctuary#20	139
