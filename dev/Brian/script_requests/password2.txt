// Can i ask about the 2nd password sceurity,
// Example i log on to my RO- i type my 1st password, username and password at log in screen,
// Then once i log in ingame, a pop up will ask 2nd password to put.

-	script	SecondPassword	-1,{
OnPCLoginEvent:
	mes "[Server]";
	// New user: create 2nd password
	if (getstrlen(#PASSWORD2_hash$) < 32) {
		while (1) {
			mes "For extra security, please create a 2nd password:";
			input .@new_password$;
			mes "Enter your 2nd password again for confirmation:";
			input .@new_password2$;
			if (.@new_password$ == .@new_password2$) break;
			mes "^FF0000 Passwords do not match!^000000";
		}
		set #PASSWORD2_hash$, md5(.@new_password$);
		mes "Thank you.";
		mes "From now on when you login, you will be asked to enter this 2nd password.";
		close;
	} else { // Returning user
		set .@tries, 3; // How many tries to enter their password?
		while (.@tries) {
			mes "Please enter your 2nd password:";
			input .@password$;
			if (md5(.@password$) == #PASSWORD2_hash$) {
				mes "Thank you.";
				close;
			}
			set .@tries, .@tries -1;
			mes "^FF0000     Invalid password.^000000";
		}
		atcommand "@kick " + strcharinfo(0); // kick them
		// atcommand "@ban 15mn " + strcharinfo(0); // ban them
	}
	end;
}
